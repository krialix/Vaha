buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.5'
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.3'
    }
}

repositories {
    google()
    jcenter()
}

def projectId = 'vahaapp-dev'

apply plugin: 'java-library'

apply plugin: 'kotlin'
apply plugin: 'kotlin-noarg'

apply plugin: 'war'

apply plugin: 'com.google.cloud.tools.appengine-standard'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'

noArg {
    annotation("com.vaha.server.common.annotations.NoArgs")
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
    mapDiagnosticLocations = true
}

final APP_ENGINE_VERSION = '1.9.63'
final JACKSON_VERSION = '2.9.4'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'javax.inject:javax.inject:1'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    // App Engine
    implementation "com.google.appengine:appengine-api-1.0-sdk:$APP_ENGINE_VERSION"

    // Endpoints
    implementation 'com.google.endpoints:endpoints-framework:2.0.13'
    //implementation 'com.google.endpoints:endpoints-management-control-appengine:1.0.5'
    implementation 'com.google.endpoints:endpoints-framework-auth:1.0.7'

    implementation 'com.google.guava:guava:24.0-jre'
    implementation 'com.googlecode.objectify:objectify:5.1.21'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'org.joda:joda-money:0.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$JACKSON_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$JACKSON_VERSION"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$JACKSON_VERSION"
    implementation 'com.google.firebase:firebase-admin:5.9.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.39'
    testImplementation "com.google.appengine:appengine-api-stubs:$APP_ENGINE_VERSION"
    testImplementation "com.google.appengine:appengine-testing:$APP_ENGINE_VERSION"
    testImplementation "com.google.appengine:appengine-tools-sdk:$APP_ENGINE_VERSION"
    testImplementation 'org.mockito:mockito-all:2.0.2-beta'
}

endpointsServer {
    // Endpoints Framework Plugin server-side configuration
    hostname = "${projectId}.appspot.com"
}

appengine {
    run {
        //port = 8888
        //jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y', '-Dappengine.fullscan.seconds=5']
    }

    deploy {
        stopPreviousVersion = true  // default - stop the current version
        promote = true              // default - & make this the current version
    }

    stage {
        enableJarClasses = true
    }
}

sourceCompatibility = javaSource
targetCompatibility = javaTarget

// this replaces the ${endpoints.project.id} in appengine-web.xml and web.xml
task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into 'build/exploded-server/WEB-INF'
    expand(endpoints: [project: [id: projectId]])
    filteringCharset = 'UTF-8'
}
assemble.dependsOn replaceProjectId
